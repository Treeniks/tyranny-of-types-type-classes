\section{Haskell}

Haskell supports both parametric and ad-hoc polymorphism.

\subsection{Parametric Polymorphism}

Parametric polymorphism is achieved the same way it is in Ocaml, that is when writing a function that does not rely on its parameters' types, then one can define the function's type with type parameters.

For example, the list length function:
\begin{minted}{haskell}
length :: [a] -> Int
length [] = 0
length (x:xs) = 1 + length xs
\end{minted}
where \verb|a| is the type parameter for the type of the list's elements.

\subsection{Ad-hoc Polymorphism: Type Classes}

Haskell developed its own system called \textit{type classes} to support ad-hoc polymorphism, because existing solutions weren't suitable in the eyes of the language designers~\cite{type-classes-original}.

In its simplest form, a type class is not much more than an interface as seen in object oriented languages:
\begin{minted}{haskell}
class Eq a where
  (==) :: a -> a -> bool
\end{minted}
Mind that it's easy to misunderstand the \verb|a| class parameter as being similar to the \verb|a| type parameter used for parametric polymorphism, however this is not the case. Where in the parametric polymorphism case before, \verb|a| stood for \textit{any} type, in this case \verb|a| stands for a \textit{class} or \textit{set} of \textit{specific} types for which instances of \verb|Eq| exist.

This also means that the type class itself so far does nothing. We need to create an instance of the type class for a specific type, for example for \verb|Int|:
\begin{minted}{haskell}
instance Eq Int where
  (==) x y = eqInt x y
\end{minted}
assuming \verb|eqInt| is the equality function for integers.

We could also create an instance for a different type, for example for \verb|Float|:
\begin{minted}{haskell}
instance Eq Float where
  (==) x y = eqFloat x y
\end{minted}
again, assuming \verb|eqFloat| is the equality function for floats.

When we now try to use the \verb|(==)| function with parameters of types other than \verb|Int| or \verb|Float|, we will get a compile time error.

\subsubsection{Type constraints}\label{constraints}

So far, we have seen how we can use type classes to overload functions defined \textit{within} the type class, however we can also use them to define standalone functions with types implementing specific type classes.

Assume we want to define some kind of cost to our data types. We can use a type class to convey this idea:
\begin{minted}{haskell}
class Cost a where
  cost :: a -> Int
\end{minted}
A possible instance for some custom data type could then look something like this:
\begin{minted}{haskell}
data Custom = Cheap | Expensive

instance Cost Custom where
  cost Cheap = 0
  cost Expensive = 1
\end{minted}

Now let's say we want to define a function \verb|cheapest| that takes a list of elements of a type that defines a \verb|cost|, and then return the cheapest element of that list. Such a function could then look like this:
\begin{minted}{haskell}
cheapest :: Cost a => [a] -> a
cheapest [x] = x
cheapest (x:xs)
  | cost x <= cost y = x
  | otherwise = y
  where y = cheapest xs
\end{minted}
The important part here is \verb|cheapest|'s type: \verb|Cost a => [a] -> a| means that \verb|cheapest| is a function of type \verb|[a] -> a| for any type \verb|a| that has an instance of \verb|Cost|.

Mind you that the similarities to the parametric polymorphism \verb|a| are a lot more fitting in this case: \verb|a| for the \verb|cheapest| function is also a type parameter where the exact type does not matter for the function. The difference is that, whereas in the case of \verb|length|, any type can be inserted into \verb|a|, for \verb|cheapest| only types that implement a \verb|cost| can be used. This is necessary as the \verb|cost| function is used in the implementation of \verb|cheapest|. This is a natural extension of the parametric polymorphism system Haskell uses and allows for much stronger expressiveness.

\subsubsection{Creating instances from other instances}

One particular strength of type classes is that they allow one to create instances based on other existing type class instances. This is best seen with an example.

We'll extend the example from \ref{constraints}:
We are given a list of \verb|Custom|s and want to determine the overall sum of the list's costs. We could write an instance for \verb|Cost [Custom]|, however this seems rather verbose, especially when assuming we have many different custom types that define a cost, and many different lists of those custom types.

Instead, we can define an instance of the \verb|Cost| type class for any list of type \verb|[a]| where \verb|a| has an instance for \verb|Cost|:
\begin{minted}{haskell}
instance Cost a => Cost [a] where
  cost = sum . map cost
\end{minted}
Now, Haskell will automatically create an instance for \verb|Cost [Custom]| the moment we create the instance for \verb|Cost Custom|.

This looks quite similar to type constraints and this is no coincidence. We are essentially doing a type constraint, only for the entire instance of a type class. We can see this as applying the type constraint to every function declared within the type class. As such, we can again use \verb|a|'s \verb|cost| function inside the \verb|Cost [a]| instance's functions.

% MAYBE mention scoping rules here, as done in Rust

Type classes also supports the notion of subclasses, however we will not cover this here as it exceeds the scope of this paper.
% MAYBE cover this anyway?

\subsubsection{Multiple Paramteres}

The default standardized Haskell definition does not allow for type classes to have more than one class parameter. However, modern implementations like \verb|ghc| allow for the \verb|-XMultiParamTypeClasses| compiler option that allow one to create type classes with multiple parameters.

The most obvious limitation this solves is the fact that, with single parameter type classes and the perspective of operator overloading, we can only define an \verb|add| function to add values of the same type. With multiple parameters, one could define an \verb|Add| type class that allows adding two values of different types.
\footnote{This is not how the \verb|add| function is implemented in standard Haskell, but this is how it \textit{could} be done. We will find this to be relevant when comparing this to Rust's traits later on.}
\begin{minted}{haskell}
class Add a b where
  add :: a -> b -> a
\end{minted}

However this is still quite ugly as currently the return type of the addition is simply the first type paramter \verb|a|. Now assuming we have a more complicated set of types where the output of adding two of them is a third independent type. An obvious fix to this would be adding a third parameter to the \verb|Add| type class like so:
\begin{minted}{haskell}
class Add a b c where
  add :: a -> b -> c
\end{minted}

While this works, it poses a strange issue: One can define multiple additions for the same type that have a different output. Let's say we define the following instances for this new \verb|Add| type class:
\begin{minted}{haskell}
instance Add Int Float Int where
  add x y = x + roundFloatInt y

instance Add Int Float Float where
  add x y = intToFloat x + y
\end{minted}
where \verb|roundFloatInt| and \verb|intToFloat| are \verb|Float| to \verb|Int| or \verb|Int| to \verb|Float| conversions respectively. At first this might seem fine, however when one were to write \verb|add 1 3.2|, the actual return type of such an expression is not well defined anymore and needs to be specialized with every call, e.g. \verb|add 1 3.2 :: Float|. This is not very ergonomic, thus a different more elegant solution exists.

% https://downloads.haskell.org/~ghc/7.8.4/docs/html/users_guide/type-class-extensions.html#:~:text=7.6.2.2.2.%C2%A0Adding%20functional%20dependencies
% FUNCTIONAL DEPENDENCIES CAN ALSO FIX THIS POTENTIALLY

\subsubsection{Associated types}

Associated Types are a way to define more class parameters, that are defined inside the instance of a type class and not in its signature. That means we can define \verb|Add| with three class parameters, where the third of those is fixed once the other two are.

In our \verb|Add| example, a solution using an associated type could look like this:
\begin{minted}{haskell}
class Add a b where
  type AddOutput a b
  add :: a -> b -> AddOutput a b
\end{minted}
Here we define that with any instance of \verb|Add a b| for specific types \verb|a| and \verb|b|, there shall also be a third type that's given the name \verb|AddOutput a b|. This type is then the output of our \verb|add| function. This concept ensures that there shall only be one well defined output type for an addition of two specific types \verb|a| and \verb|b|.

To define an addition for \verb|Int| and \verb|Float|, this means we have to decide what output we would even want in this case. Let's assume the choice was made to output a \verb|Float| in this case, the instance for the \verb|Add| type class would then look like this:
\begin{minted}{haskell}
instance Add Int Float where
  type AddOutput Int Float = Float
  add x y = intToFloat x + y
\end{minted}
One unfortunate limitation is that the \verb|AddOutput| name is not namespaced, i.e. we will need to have a different name for each associated type we have inside of different type classes.

\subsubsection{A matrix example}

Let's assume we have a data type called \verb|Matrix a| which describes a matrix whose elements are of type \verb|a|. We can then use our previously defined \verb|Add| type class to define a matrix addition for any types of matrices that should be addable. We will ignore the matrices' size constraints for them to be addable, as well as the implementation of the action.
\begin{minted}{haskell}
instance Add a b => Add (Matrix a) (Matrix b) where
  type AddOutput (Matrix a) (Matrix b) =
    Matrix (AddOutput a b)
  add x y = -- ...
\end{minted}
The first line can be read as "implement \verb|Add| for two matrices whose elements' types implement \verb|Add|". The second and third lines mean "the output of such an addition shall be another matrix whose elements' type is that of the output of adding the elements of the original two matrices together".

To make this clear again: This is \textbf{not} how Haskell typically implements overloaded addition. Instead, Haskell defines a \verb|Num| type class that defines multiple operators, such as \verb|(+)| and the unary negate, and only defines those operations on two elements \textit{of the same type}. What we did here was merely to showcase the strength of an overloading system that allows for multiple parameters and associated types, and how operator overloading could be solved using these.
