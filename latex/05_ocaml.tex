\section{OCaml}

As already noted before, OCaml supports parametric polymorphism. However the language has no concept of ad-hoc polymorhism, i.e. there is no way to overload functions. Nontheless, OCaml inherits ML's powerful module system which still allows the language to express and convey complicated relations between types.

At first glance, a module system might not seem related to interface systems. However these concepts aren't all that different. Interfaces define a set of functions over one or, in some languages, multiple types. Modules are quite similar, they are a collection of types and functions. For example, to define a module for working with matrices of integers, in OCaml one could write this:
\begin{minted}{ocaml}
module DenseMatrix =
  struct
    type elem = int
    type t = elem list list
    let add a b = (* ... *)
  end
\end{minted}
This matrix module could be further extended with other functions, but for our example this minimal version suffices.

% TODO this \verb|elem list list| may cause problems at end of the line
OCaml modules will also have an associated \textit{signature}. The signature includes information about the function names and types, the names of the defined types and optionally what those types are defined to. The latter part is optional to have the ability to hide implementation details from the user of the module. OCaml can infer the signature of our \verb|DenseMatrix| module, but let's say we want to hide the fact that our matrix uses an \verb|elem list list|, so we'll define the signature ourselves:
\begin{minted}{ocaml}
module type Matrix =
  sig
    type elem = int
    type t
    val add : t -> t -> t
  end
module DenseMatrix : Matrix =
  struct
    type elem = int
    type t = elem list list
    let add a b = (* ... *)
  end
\end{minted}
This also allows us to create a separate \verb|SparseMatrix| module with the same signature:
\begin{minted}{ocaml}
module SparseMatrix : Matrix =
  struct
    type elem = int
    type t = (* ... *)
    let add a b = (* ... *)
  end
\end{minted}
Thus, from the perspective of interfaces, module signatures can be seen as type classes and modules as instances~\cite{modular-type-classes}.

The main difference to type classes (and by extension the reason why OCaml modules are not ad-hoc polymorphic) is that the \verb|add| function will be namespaced inside each module we defined. If we wanted to use the \verb|DenseMatrix|'s add function, we would have to specify it as \verb|DenseMatrix.add|. There is no logic behind automatically choosing the correct \verb|add| function for a given context, which is what ad-hoc polymorphism and overloading is about. Instead, the caller must specify an exact function they want to call. In contrast to this, for Haskell, the implemented functions in a type class were available independently from specific instances, i.e. in OCaml's terms, the module signature's defined functions could be used without specifying the exact module.

\subsection{Functors}

\subsubsection{Extending our matrices for more element types}

Let's take one step back and slightly alter our \verb|Matrix| module signature.
\begin{minted}{ocaml}
module type Matrix =
  sig
    type elem
    type t
    val add : t -> t -> t
  end
\end{minted}
In particular, we are not defining the type of \verb|elem| anymore. This allows us to implement multiple matrices for different types.

\begin{figure*}
\begin{minted}{ocaml}
module DenseIntMatrix : (Matrix with type elem = int) =
  struct
    type elem = int
    type t = elem list list
    let add a b = (* ... *)
  end

module SparseFloatMatrix : (Matrix with type elem = float) =
  struct
    type elem = float
    type t = (* ... *)
    let add a b = (* ... *)
  end
\end{minted}
\caption{OCaml matrix example with dense and sparse matrix}
\label{ocaml-all-matrix}
\end{figure*}

\begin{figure*}
\begin{minted}{ocaml}
module DenseMatrix (D : Addable) : (Matrix with type elem = D.t) =
  struct
    type elem = D.t
    type t = elem list list
    let add a b = (* implementation of Matrix addition using D.add *)
  end
\end{minted}
\caption{OCaml functor matrix example}
\label{ocaml-matrix-functor}
\end{figure*}

\autoref{ocaml-all-matrix} shows two of many possible matrix modules. \\ \verb|Matrix with type elem = int| simply means to replace \verb|type elem| with \verb|type elem = int| inside the \verb|Matrix| module signature, effectively exporting the type of \verb|elem|. This allows an outside user of \verb|DenseIntMatrix| to know that \verb|elem| is an \verb|int|. This can be important if, let's say, we added a \verb|get| function that returns an \verb|elem| to get certain elements out of the matrix. If we didn't specify that \verb|elem| was of type \verb|int|, the user of our module would then not know that \verb|get| returns \verb|int|s, but rather some unknown and thus unusable type \verb|elem|.

Because we left out the implementations of these functions, it might not be immediately obvious, but the actual implementation of a dense matrix with \verb|float| values and a dense matrix with \verb|int| values will most likely look \textit{very} similar. The same is true for the sparse matrix. This is not exactly something we want, and this is where functors come in handy.

\subsubsection{Defining addition as a module}

Functors, as the name would suggest, are something similar to functions. In a sense, they can be seen as compile-time functions that take modules as arguments, and return other modules. They can also be seen as a sort of macro for module definitions.

A functor in our case could define the implementation of an entire \verb|DenseMatrix| for any type \verb|elem| that implements an addition. Thus we must first define what "implements an addition" means, which we do by defining a module signature:
\begin{minted}{ocaml}
module type Addable =
  sig
    type t
    val add : t -> t -> t
  end
\end{minted}
Because our \verb|Matrix| module signature defines this very \verb|add| function, we can also define the \verb|Matrix| signature by including \verb|Addable|:
\begin{minted}{ocaml}
module type Matrix =
  sig
    type elem
    type t
    include Addable with type t := t
  end
\end{minted}
The \verb|include| keyword in OCaml copies all definitions of another module/signature into the current module/signature, making our matrices addable in this case. The reason why we don't use \verb|Addable|'s \verb|type t| but instead our own is because we might want to add signatures for other operators later on which should all share the same \verb|t|.

\subsubsection{Functors in action}

\autoref{ocaml-matrix-functor} shows how we can define a \verb|DenseMatrix| functor, that takes in an \verb|Addable| module as an argument called \verb|D|, and gives back a \verb|Matrix| module by implementing a dense matrix with elements of type \verb|D.t|. We can do the same for sparse matrices, but we will skip this here.

So far we can't use the \verb|DenseMatrix| functor, as we have yet to create a module with the \verb|Addable| signature, so let's do so for \verb|int|s:
\newpage
\begin{minted}{ocaml}
module IntAddable : (Addable with type t = int) =
  struct
    type t = int
    let add a b = a + b
  end
\end{minted}
Now we can create a \verb|DenseIntMatrix| module by calling the \\ \verb|DenseMatrix| functor on our \verb|IntAddable| module:
\begin{minted}{ocaml}
module DenseIntMatrix = DenseMatrix IntAddable
\end{minted}
A \verb|DenseMatrix| for floating point values can easily be created as well, simply by creating a \verb|FloatAddable| module and calling the \verb|DenseMatrix| functor on it.
